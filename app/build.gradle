plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "kotlin-parcelize"
    id "com.mikepenz.aboutlibraries.plugin"
}

def verCode = 22
def verName = "0.1-beta22"

def keystorePwd = null
def alias = null
def pwd = null
def packageName = null

Properties properties
def base64 = System.getenv("LOCAL_PROPERTIES")
if (base64 != null && !base64.isBlank()) {
    properties = new Properties()
    properties.load(new ByteArrayInputStream(Base64.decoder.decode(base64)))
} else if (project.rootProject.file("local.properties").exists()) {
    properties = new Properties()
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

if (properties != null) {
    keystorePwd = properties.getProperty("KEYSTORE_PASS")
    alias = properties.getProperty("ALIAS_NAME")
    pwd = properties.getProperty("ALIAS_PASS")
    packageName = properties.getProperty("PACKAGE_NAME")
}

keystorePwd = keystorePwd ?: System.getenv("KEYSTORE_PASS")
alias = alias ?: System.getenv("ALIAS_NAME")
pwd = pwd ?: System.getenv("ALIAS_PASS")

def serviceAccountCredentialsFile = rootProject.file("service_account_credentials.json")
def beta = verName.contains("beta")
if (serviceAccountCredentialsFile.isFile()) {
    setupPlay(beta)
    play.serviceAccountCredentials = serviceAccountCredentialsFile
} else if (System.getenv().containsKey("ANDROID_PUBLISHER_CREDENTIALS")) {
    setupPlay(beta)
}

void setupPlay(boolean beta) {
    apply plugin: "com.github.triplet.play"
    play {
        track = "beta"
        //    track = beta ? "beta" : "production"
        defaultToAppBundles = true
    }
}

configurations {
    compile.exclude module: "support-v4"
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId packageName == null ? "io.nekohasekai.sagernet" : packageName
        minSdkVersion 21
        targetSdkVersion 30
        versionCode verCode
        versionName verName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild.ndkBuild {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            arguments "-j${Runtime.getRuntime().availableProcessors()}"
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    externalNativeBuild.ndkBuild.path "src/main/jni/Android.mk"

    signingConfigs {
        release {
            storeFile rootProject.file("release.keystore")
            storePassword keystorePwd
            keyAlias alias
            keyPassword pwd
        }
    }

    buildTypes {
        debug {
            signingConfig keystorePwd == null ? signingConfigs.debug : signingConfigs.release
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig keystorePwd == null ? signingConfigs.debug : signingConfigs.release
        }
    }

    flavorDimensions "version"

    productFlavors {
        expert {
            // applicationIdSuffix ".expert"
        }
        oss {
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        coreLibraryDesugaringEnabled true
    }

    lintOptions {
        disable "MissingTranslation"
        disable "ExtraTranslation"
        disable "BlockedPrivateApi"
    }

    packagingOptions {
        exclude "/META-INF/*.version"
        exclude "/META-INF/*.kotlin_module"
        exclude "/META-INF/native-image/**"
        exclude "/META-INF/INDEX.LIST"
        exclude "DebugProbesKt.bin"
        exclude "/kotlin/**"
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    bundle {
        language {
            enableSplit = false
        }
    }

    splits {
        abi {
            enable true
            universalApk false
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = outputFileName
                    .replace("app", "SN-" + variant.versionName)
                    .replace("-release", "")
                    .replace("-oss", "")
        }
    }

}

dependencies {

    implementation fileTree(dir: "libs")
    implementation project(":plugin")

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0-RC"
    implementation "androidx.activity:activity-ktx:1.2.2"
    implementation "androidx.fragment:fragment-ktx:1.3.3"
    implementation "androidx.browser:browser:1.3.0"

    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation project(":preferencex")
    implementation project(":preferencex-simplemenu")

    implementation "com.google.android.material:material:1.3.0"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "cn.hutool:hutool-core:5.6.4"
    implementation "cn.hutool:hutool-json:5.6.4"
    implementation "cn.hutool:hutool-crypto:5.6.4"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.google.zxing:core:3.4.1"
    implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.2"
    implementation "org.yaml:snakeyaml:1.28"
    implementation "com.github.daniel-stoneuk:material-about-library:3.2.0-rc01"
    implementation "com.mikepenz:aboutlibraries:8.8.5"

    implementation("com.simplecityapps:recyclerview-fastscroll:2.0.1") {
        exclude group: "androidx.recyclerview"
        exclude group: "androidx.appcompat"
    }
    implementation("org.smali:dexlib2:2.5.2") {
        exclude group: "com.google.guava", module: "guava"
    }
    implementation "com.google.guava:guava:30.1.1-android"
    implementation "com.journeyapps:zxing-android-embedded:4.2.0"

    implementation "androidx.room:room-runtime:2.3.0"
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"

    implementation "com.github.MatrixDev.Roomigrant:RoomigrantLib:0.3.4"
    kapt "com.github.MatrixDev.Roomigrant:RoomigrantCompiler:0.3.4"

    implementation "com.esotericsoftware:kryo:5.1.1"

    testImplementation "junit:junit:4.13.2"
    testImplementation "androidx.room:room-testing:2.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.2"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.5"

}